; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Husarnet"
#define MyAppVersion "2021.06.18.3"
#define MyAppPublisher "Husarnet Sp. z o.o."
#define MyAppURL "https://husarnet.com"
#define MyAppExeName "husarnet-gui.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{843C2149-4374-43E5-8D8E-DFD68C16F5AA}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DisableDirPage=yes
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
; OutputDir=E:\husarnet\installer
OutputBaseFilename=husarnet-setup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
UsePreviousAppDir=yes
LicenseFile=license.txt
CloseApplications=force
CloseApplicationsFilter=husarnet-gui.exe

ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Code]
procedure BeforeReplaceService();
var
  ResultCode: Integer;
begin
  if Exec(ExpandConstant('{app}\nssm.exe'), 'stop husarnet', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
  begin
    Log('service stop ok');
  end
  else begin
    Log('service stop not ok');
  end
end;

[Files]
Source: "husarnet.exe"; DestDir: "{app}"; Flags: ignoreversion; BeforeInstall: BeforeReplaceService
Source: "addtap.bat"; DestDir: "{app}"; Flags: ignoreversion

Source: "husarnet-gui.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "tap-windows.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "nssm.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "installservice.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "uninstallservice.bat"; DestDir: "{app}"; Flags: ignoreversion

Source: "*.o"; DestDir: "{app}"; Flags: ignoreversion
Source: "*.png"; DestDir: "{app}"; Flags: ignoreversion

[Run]
Filename: "{app}\tap-windows.exe"; Parameters: "/S"; StatusMsg: "Installing TAP driver..."

[Run]
Filename: "{app}\installservice.bat"; StatusMsg: "Installing service..."; Flags: runhidden

[UninstallRun]
Filename: "{app}\uninstallservice.bat"; StatusMsg: "Uninstalling service..."; Flags: runhidden

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{commonstartup}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Parameters: "--autostart"

;[Run]
;Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
